;;; ============================================================
;;; FLYING.TOASTERS - Desk Accessory
;;;
;;; Clears the screen and animates a pleasing distraction.
;;; ============================================================

        .include "../config.inc"

        .include "apple2.inc"
        .include "../inc/macros.inc"
        .include "../mgtk/mgtk.inc"
        .include "../common.inc"
        .include "../desktop/desktop.inc"

;;; ============================================================

        DA_HEADER
        DA_START_AUX_SEGMENT

;;; ============================================================

.proc AuxStart
        ;; Run the DA
        jmp     Init
.endproc ; AuxStart

;;; ============================================================
;;; Animation Resources

        kToasterHeight = 33
        kToasterWidth  = 70

        kToasterCount = 4

xpos_table:
        .word   kScreenWidth+kToasterWidth
        .word   kScreenWidth+kToasterWidth+150
        .word   kScreenWidth+kToasterWidth+300
        .word   kScreenWidth+kToasterWidth+450

ypos_table:
        .word   AS_WORD(-kToasterHeight)
        .word   AS_WORD(-kToasterHeight)+160
        .word   AS_WORD(-kToasterHeight)+40
        .word   AS_WORD(-kToasterHeight)+99

frame_table:
        .byte   0,1,2,3

;;; ============================================================
;;; Graphics Resources

event_params:   .tag MGTK::Event


.params paintbits_params
        DEFINE_POINT viewloc, 0, 0
mapbits:        .addr   0
mapwidth:       .byte   10
reserved:       .byte   0
        DEFINE_RECT maprect, 0, 0, kToasterWidth-1, kToasterHeight-1
        REF_MAPINFO_MEMBERS
.endparams

notpencopy:     .byte   MGTK::notpencopy
pencopy:        .byte   MGTK::pencopy

grafport:       .tag MGTK::GrafPort

;;; ============================================================
;;; DA Init

.proc Init
        MGTK_CALL MGTK::HideCursor

        MGTK_CALL MGTK::InitPort, grafport
        MGTK_CALL MGTK::SetPort, grafport
        MGTK_CALL MGTK::SetPenMode, notpencopy
        MGTK_CALL MGTK::PaintRect, grafport + MGTK::GrafPort::maprect

        MGTK_CALL MGTK::FlushEvents
        FALL_THROUGH_TO InputLoop
.endproc ; Init

;;; ============================================================
;;; Main Input Loop

.proc InputLoop
        MGTK_CALL MGTK::GetEvent, event_params
        lda     event_params + MGTK::Event::kind
        cmp     #MGTK::EventKind::button_down ; was clicked?
        beq     exit
        cmp     #MGTK::EventKind::key_down  ; any key?
        beq     exit

        jsr     Animate
        jmp     InputLoop

exit:
        MGTK_CALL MGTK::RedrawDeskTop

        MGTK_CALL MGTK::DrawMenuBar
        JSR_TO_MAIN JUMP_TABLE_HILITE_MENU

        MGTK_CALL MGTK::ShowCursor
        rts                     ; exits input loop
.endproc ; InputLoop

;;; ============================================================
;;; Animate

.proc Animate
        MGTK_CALL MGTK::SetPort, grafport
        MGTK_CALL MGTK::SetPenMode, pencopy

        ;; For each toaster...
        copy8   #kToasterCount-1, index
        ;; Stash current toaster's values
    DO
        ldx     index
        copy8   frame_table,x, frame
        txa
        asl
        tax
        copy16  xpos_table,x, xpos
        copy16  ypos_table,x, ypos

        ;; Move
        add16   ypos, #1, ypos
        sub16   xpos, #4, xpos

        ;; Wrap Y
        cmp16   ypos, #kScreenHeight
      IF VS
        eor     #$80
      END_IF
      IF NC
        copy16  #AS_WORD(-kToasterHeight), ypos
      END_IF

        ;; Wrap X
        cmp16   xpos, #AS_WORD(-kToasterWidth)
      IF VS
        eor     #$80
      END_IF
      IF NS
        copy16  #kScreenWidth+kToasterWidth, xpos
      END_IF

        ;; Next frame
        inc     frame
        lda     frame
      IF A = #4                 ; num frames
        copy8   #0, frame
      END_IF

        ;; Draw new pos
        copy16  xpos, paintbits_params::viewloc::xcoord
        copy16  ypos, paintbits_params::viewloc::ycoord
        lda     frame
        asl                     ; *2
        tax
        copy16  toaster_frames,x, paintbits_params::mapbits
        MGTK_CALL MGTK::PaintBits, paintbits_params

        ;; Store updated values
        ldx     index
        copy8   frame, frame_table,x
        txa
        asl
        tax
        copy16  xpos, xpos_table,x
        copy16  ypos, ypos_table,x

        ;; Next
        dec     index
    WHILE POS
        rts

index:  .byte   0
xpos:   .word   0
ypos:   .word   0
frame:  .byte   0

.endproc ; Animate


;;; ============================================================

toaster_frames:
        .addr   toaster_bits1
        .addr   toaster_bits2
        .addr   toaster_bits3
        .addr   toaster_bits2

toaster_bits1:
        PIXELS  "......................................................................"
        PIXELS  "................######..##............................................"
        PIXELS  "..............######..####............................................"
        PIXELS  "............####..######..##....########.............................."
        PIXELS  "..........############..######################........................"
        PIXELS  "..........########..########..............########...................."
        PIXELS  "........##############............####################................"
        PIXELS  "........##########..........########..............######.............."
        PIXELS  "......########..........######............######........##............"
        PIXELS  "......####..........######..........######......################......"
        PIXELS  "....####........######..........########..##########..##..##.........."
        PIXELS  "..####........####..........##########..######..################......"
        PIXELS  "..########..####........############..############..##..##............"
        PIXELS  "##....########........####..########..######..################........"
        PIXELS  "##........########..####..########..############..##..##.............."
        PIXELS  "##..####......########..##########..######..##############............"
        PIXELS  "##..##..####......####..########..############........................"
        PIXELS  "##..##......####..##..##########..####..##########..##................"
        PIXELS  "##..........####..##..########..##########........####..##............"
        PIXELS  "##......##..####..##..########..####..########..####.................."
        PIXELS  "##................##....##..##..########......######....##............"
        PIXELS  "##......##........##....########..####..##########...................."
        PIXELS  "##................##....##..######....##########........##............"
        PIXELS  "##......##........##......##....############..........##.............."
        PIXELS  "##................##........############............##................"
        PIXELS  "##......##........##............................####.................."
        PIXELS  "##................##........................####......................"
        PIXELS  "..##..............##....................####.........................."
        PIXELS  "....##............##................####.............................."
        PIXELS  "......####........##..........######.................................."
        PIXELS  "..........####....##....######........................................"
        PIXELS  "..............##########.............................................."

toaster_bits2:
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "....................####........########.............................."
        PIXELS  "................########..####################........................"
        PIXELS  "..............####..########..............########...................."
        PIXELS  "............##########............####################................"
        PIXELS  "..........########..........########..............######.............."
        PIXELS  "........######..........######............################............"
        PIXELS  "......####..........######..........##########........####............"
        PIXELS  "....####........######..........##########....########..##............"
        PIXELS  "..####........####..........##############################............"
        PIXELS  "..########..####........##################..............##............"
        PIXELS  "##....########........####..##########....##############.............."
        PIXELS  "##........########..####..##########..##########..##############......"
        PIXELS  "##..####......########..##########..################..##..##.........."
        PIXELS  "##..##..####......####..########..############..################......"
        PIXELS  "##..##......####..##..##########..################..##....####........"
        PIXELS  "##..........####..##..########..############..############............"
        PIXELS  "##......##..####..##..########..####..##..######..##.................."
        PIXELS  "##................##....##..##..########................##............"
        PIXELS  "##......##........##....########..####..##########...................."
        PIXELS  "##................##....##..######....##########........##............"
        PIXELS  "##......##........##......##....############..........##.............."
        PIXELS  "##................##........############............##................"
        PIXELS  "##......##........##............................####.................."
        PIXELS  "##................##........................####......................"
        PIXELS  "..##..............##....................####.........................."
        PIXELS  "....##............##................####.............................."
        PIXELS  "......####........##..........######.................................."
        PIXELS  "..........####....##....######........................................"
        PIXELS  "..............##########.............................................."

toaster_bits3:
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "......................................................................"
        PIXELS  "..................................########............................"
        PIXELS  "..........................####################........................"
        PIXELS  "......................########..............########.................."
        PIXELS  "................######............####################................"
        PIXELS  "............######..........########..............######.............."
        PIXELS  "........######..........######............################............"
        PIXELS  "......####..........######..........##########........####............"
        PIXELS  "....####........######..........##########....########..##............"
        PIXELS  "..####........####..........##############################............"
        PIXELS  "..########..####........##################################............"
        PIXELS  "##....########........####..############################.............."
        PIXELS  "##........########..####..############################..##............"
        PIXELS  "##..####......########..##############################................"
        PIXELS  "##..##..####......####..##########....############......##............"
        PIXELS  "##..##......####..##..##########..####..............####..####........"
        PIXELS  "##..........####..##..########..##########..####..####..####..##......"
        PIXELS  "##......##..####..##..########..########..####..####..####..####......"
        PIXELS  "##................##....##..##..############..####..####..####........"
        PIXELS  "##......##........##....########..####################..######........"
        PIXELS  "##................##....##..######..##########..############.........."
        PIXELS  "##......##........##......##....####..############..########.........."
        PIXELS  "##................##........##########..##################............"
        PIXELS  "##......##........##......................##############.............."
        PIXELS  "##................##........................##########................"
        PIXELS  "..##..............##....................####....####.................."
        PIXELS  "....##............##................####.............................."
        PIXELS  "......####........##..........######.................................."
        PIXELS  "..........####....##....######........................................"
        PIXELS  "..............##########.............................................."

;;; ============================================================

        DA_END_AUX_SEGMENT

;;; ============================================================

        DA_START_MAIN_SEGMENT

        JSR_TO_AUX aux::AuxStart
        rts

        DA_END_MAIN_SEGMENT

;;; ============================================================
